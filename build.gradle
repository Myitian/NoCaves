plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.11-SNAPSHOT" apply false
}

ext {
    props = [
            mod_id                              : project.mod_id,
            mod_version                         : project.mod_version,
            mod_name                            : project.mod_name,
            mod_description                     : project.mod_description,
            mod_license                         : project.mod_license,
            mod_author                          : project.mod_author,
            mod_source                          : project.mod_source,
            mod_icon                            : project.mod_icon,
            maven_group                         : project.maven_group,
            supported_minecraft_version_fabric  : project.supported_minecraft_version_fabric,
            supported_minecraft_version_forge   : project.supported_minecraft_version_forge,
            supported_minecraft_version_neoforge: project.supported_minecraft_version_neoforge,
    ]
}

architectury {
    minecraft = rootProject.main_minecraft_version
}

def customFix = file("fix.tiny")

subprojects {
    apply plugin: "dev.architectury.loom"

    dependencies {
        switch (project.name) {
            case "variant":
            case "variant-fabric-wrapper":
            case "variant-forge-wrapper":
            case "variant-neoforge-wrapper":
                minecraft "com.mojang:minecraft:${project.variant_minecraft_version}"
                mappings loom.layered {
                    it.mappings("net.fabricmc:yarn:${project.variant_yarn_mappings}:v2")
                    it.mappings("dev.architectury:yarn-mappings-patch-neoforge:${project.yarn_mappings_patch_neoforge_version}")
                }
                break
            default:
                minecraft "com.mojang:minecraft:${project.main_minecraft_version}"
                mappings loom.layered {
                    it.mappings("net.fabricmc:yarn:${project.main_yarn_mappings}:v2")
                    it.mappings("dev.architectury:yarn-mappings-patch-neoforge:${project.yarn_mappings_patch_neoforge_version}")
                }
                break
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"
    apply plugin: "maven-publish"

    version = project.mod_version
    group = project.maven_group

    repositories {
        maven { url "https://maven.shedaniel.me" }
        maven { url "https://maven.architectury.dev" }
        maven { url "https://maven.neoforged.net" }
        maven { url "https://maven.parchmentmc.org" }
        maven { url "https://maven.terraformersmc.com" }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release = 21
    }

    java {
        withSourcesJar()
    }
}
