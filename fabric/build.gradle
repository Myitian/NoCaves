plugins {
    id "com.gradleup.shadow" version "9.2.2"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").file("src/main/resources/${project.mod_id}.accesswidener")
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

shadow {
    addShadowVariantIntoJavaComponent = false
}

base {
    archivesName = "${project.mod_id}-${project.name}-${project.version_id}"
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${project.fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_api_version}"
    modImplementation "com.terraformersmc:modmenu:${project.mod_menu_version}"
    modImplementation "me.shedaniel.cloth:cloth-config-fabric:${project.main_cloth_config_version}"

    common(project(path: ":common", configuration: "namedElements")) { transitive false }
    shadowCommon(project(path: ":common", configuration: "transformProductionFabric")) { transitive false }
    common(project(":variant-fabric-wrapper")) { transitive false }
    shadowCommon(project(":variant-fabric-wrapper")) { transitive false }
}

processResources {
    def propsCopy = new HashMap(props)
    propsCopy.replace("mod_version", "${rootProject.mod_version}-${project.name}-${rootProject.version_id}")
    inputs.properties propsCopy

    filesMatching("fabric.mod.json") {
        expand propsCopy
    }

    from(project(":common").file("src/main/resources")) {
        include "${rootProject.mod_id}.accesswidener"
    }
}

shadowJar {
    exclude "architectury.common.json"
    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

publishing {
    publications {
        mavenFabric(MavenPublication) {
            artifactId = rootProject.mod_id + "-" + project.name
            from components.java
        }
    }
    repositories {
    }
}